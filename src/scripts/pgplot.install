#! /bin/csh -f
#
#  Install PGPLOT from $NEMO/local/pgplot into $NEMOLIB
#
#  3-apr-01	allowed final directory also to be configurable
#		and some NEMO V3 support added
#  9-apr-01     added support if file= is given to directly specify pgplot tar ball
# 11-jun-01     for new pgplot file name convention (520, 521, 522....)
# 11-jan-02     also allow pgplot directory in $NEMO/local/pgplot
#  9-apr-02     fixed caltech links
#  5-dec-02     added tcl/tk driver for carma
# 29-jun-03     added darwin (tested w/ MacOSX 10.2)
# 23-jan-04     fixed linux when CC and F77 present (e.g. intel compiler)
# 27-mar-04     force rebuilding some executables upon reload (+typo fix)


#			 	.. command line keywords ..
set version=522
set ftp=ftp.astro.caltech.edu
set dir=$NEMO/local
set lib=$NEMOLIB
set drivers=CGDRIV,GIDRIV,NUDRIV,PGDRIV,PPDRIV,PSDRIV,WDDRIV,XWDRIV,TKDRIV
set host=$NEMOHOST

#  dir:  where the source code will be extracted
#  lib:  where the library (PGPLOT_DIR) will be placed

#				.. parse command line ..
foreach arg ($*)
   set check = `echo $argv[1] | awk -F= '{print NF}'`
   if ("$check" == 1) break
   echo Setting: $arg
   set $arg
end


if (! -e $lib) mkdir $lib
if ($status) exit 1

cd $NEMO/local

if (-d pgplot) then
  echo Assuming your $NEMO/local/pgplot is current
else
  if ($?file == 0) set file=$dir/pgplot${version}.tar.gz
  if (! -e $file) then
    echo No file $file, you need to pick it up, e.g.
    echo ncftp ftp://ftp.astro.caltech.edu/pub/pgplot/pgplot${version}.tar.gz
    exit 1
  else
    echo Found $file
  endif
  gunzip -dc $file | tar xf -
endif

cd pgplot
cp drivers.list $lib

cd $lib

cp drivers.list new.list
cp drivers.list old.list

echo Building drivers for $drivers

foreach d (`echo $drivers | sed 's/,/ /g'`)
   sed 's/\! '$d/$d/g < old.list > new.list
   cp new.list old.list
end

mv new.list drivers.list
rm old.list

echo Renaming old X server to force it to rebuild
if (-e pgxwin_server) mv pgxwin_server pgxwin_server.old

# Solaris
switch ($host)
  case *darwin*:
    $NEMO/local/pgplot/makemake   $NEMO/local/pgplot darwin g77_gcc
    breaksw
  case solaris*:
  case sparc*:
  case sun5:
    if ($?CC && $?F77) then
      echo ${F77}_${CC}
      $NEMO/local/pgplot/makemake  $NEMO/local/pgplot sol2 ${F77}_${CC}
    else 
      $NEMO/local/pgplot/makemake  $NEMO/local/pgplot sol2 f77_cc
    endif
    breaksw
  case linux:
  case *linux-gnu:
    if ($?CC && $?F77) then
        $NEMO/local/pgplot/makemake  $NEMO/local/pgplot linux ${F77}_${CC}
    else
        $NEMO/local/pgplot/makemake  $NEMO/local/pgplot linux g77_gcc
    endif
    if (! -e /usr/X11) then
        echo You may need to patch your Linux distribution:
        echo ln -s /usr/X11R6 /usr/X11
    endif
    breaksw
  case sgi:
    $NEMO/local/pgplot/makemake  $NEMO/local/pgplot irix
    breaksw
  default:
    echo host=$host not supported yet
    exit 1
endsw

make -i
make -i cpg
make -i clean
\rm -fr pgdemo*

#  may also need to change LD_LIBRARY_PATH with $NEMOLIB

# set PGPLOT_DIR to $NEMOLIB

