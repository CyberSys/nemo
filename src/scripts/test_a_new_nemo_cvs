#! /bin/csh -f
#
#  P600/     116.510u 17.390s 2:41.66 82.8%     0+0k 0+0io 778435pf+0w
# P1600/
#
#  History:     29-mar-2004   for NEMO V3.2

set pgplot=1
set intel=0
set gcc=0
set falcon=1
set anonymous=1
set cvsroot=0
set vogl=1
set branch=()

foreach _a ($*)
  set $_a
end
  
if ($?NEMO) then
  echo Cannot test if NEMO=$NEMO is present already
  exit 1
endif

if ($anonymous) then
 setenv CVSROOT :pserver:anonymous@cvs.astro.umd.edu:/home/cvsroot
endif

#if ($?CVSROOT == 0) then
#
#endif

#		pick another GNU compiler 
if ($gcc) then
   source /astromake/astromake_start
   astroload -v $gcc gcc
endif

#		pick intel compiler (ifort means >= 8.0)
if ($intel) then
  source /astromake/astromake_start
  astroload intel
  setenv CC icc
  setenv CXX icpc
  setenv F77 ifort
endif

#		pick a certain CVS release, if requested
if ($#branch) then
  echo Using branch $branch
  set cvs_rev=(-r $branch)
else
  echo Using default MAIN branch
  set cvs_rev=()
endif

#		get  the source
time cvs -Q checkout $cvs_rev nemo
cd nemo

if ($pgplot) then   
  (mkdir local; cd local; cvs -Q checkout pgplot)
endif


set copts=()
# set copts=($copts --disable-gsl)
# set copts=($copts --enable-single)
if ($pgplot) set copts=($copts --with-yapp=pgplot --with-pgplot-prefix=`pwd`/lib)

./configure  $copts
source nemo_start
make postconfig

source NEMORC.local
if ($pgplot) then
  echo "Compiling internal pgplot (logfile: install.pgplot.log)"
  make pgplot >& install.pgplot.log
endif
rehash
make libs
if ($vogl) then
  echo "Compiling vogl (logfile: install.vogl.log)"
  $NEMO/src/scripts/nemo.vogl >& install.vogl.log
endif

src/scripts/testsuite -b

if ($falcon) then
if (-d usr/dehnen/falcON) then
  # falcON
  set fbins=(TestGrav gyrfalcON)
  set arch=i386_linux
  echo Compiling $fbins for $arch
  ( cd usr/dehnen/falcON; make ; make $fbins; cd $arch; cp $fbins $NEMOBIN)  >& install_falcON.log
else
  # YancNemo
  echo Compiling YancNemo
  ( cd usr/dehnen/yanc; mkdir -p lib; make ; make YancNemo mkking; cp YancNemo mkking $NEMOBIN )  >& install_yanc.log
endif
endif

# src/scripts/nemo.vogl

echo All done.
