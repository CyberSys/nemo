#! /bin/csh -f
#
#   Conversion script from versions before nemo.32 to nemo.32
#   where NEMO/{lib,bin,obj} do not have the NEMOHOST subdirectories
#   anymore.
#
#   28-mar-04   Created   -   Peter Teuben  
#


#     check NEMO
if ($?NEMO == 0) then
  echo No NEMO environment present yet
  exit 0
else
  echo Found NEMO=$NEMO
endif

cd $NEMO

#     check config.status if there was an installation
if (! -e config.status) then
   echo NEMO was never installed. Cannot patch this system, 
   echo please cvs update your system first and install it
   exit 0
endif

#     check if the directories exist
foreach d (bin lib obj) 
  if (! -d $NEMO/$d/$NEMOHOST) then
    echo No $NEMO/$d/$NEMOHOST
    exit 0
  else
    echo Found $NEMO/$d/$NEMOHOST 
  endif
end

#   move the contents of the directories in the right place
foreach d (bin lib obj) 
  mv $NEMO/$d/$NEMOHOST/* $NEMO/$d
  rmdir $NEMO/$d/$NEMOHOST
end

#   create the new opt tree, and 
#   move the old style host tree out of the way
cd $NEMO
./config.status
make dirs
mv host host_31


echo Successfully modified your NEMO tree. Your current shell
echo does not function anymore, please issue the following
echo commands:
echo "   omen"
echo "   nemo"
echo or:
echo "   cd $NEMO"
echo "   source nemo_end"
echo "   source nemo_start"
echo ""

